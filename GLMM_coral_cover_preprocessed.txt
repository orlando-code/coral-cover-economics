
    model{
    #1A. Priors for fixed effects (vague priors: precision 0.0001 = SD 100)
    for (i in 1:K) { beta[i] ~ dnorm(0, 0.0001) }
    
    # Hierarchical random effects
    for (i in 1:Nre) { a[i] ~ dnorm(ecoregion[region_for_each_site[i]], tau) }
    
    # Ecoregion effects with diversity predictor
    for(z in 1:R){
      ecoregion[z] ~ dnorm(g[z], tau_ecoregion)
      g[z] <- mu_global + beta_diversity * diversity[z]
    }
    
    mu_global ~ dnorm(0, 0.0001)      # Global mean prior
    beta_diversity ~ dnorm(0, 0.0001) # Diversity slope prior
    
    #1B. Half-Cauchy(25) prior for site-level SD
    num ~ dnorm(0, 0.0016) 
    denom ~ dnorm(0, 1)
    sigma <- abs(num / denom)
    tau <- 1 / (sigma * sigma)
    
    #1C. Half-Cauchy(25) prior for ecoregion-level SD
    num_ecoregion ~ dnorm(0, 0.0016) 
    denom_ecoregion ~ dnorm(0, 1)
    sigma_ecoregion <- abs(num_ecoregion / denom_ecoregion)
    tau_ecoregion <- 1 / (sigma_ecoregion * sigma_ecoregion)
    
    #1D. Half-Cauchy(25) prior for precision parameter theta
    numtheta ~ dnorm(0, 0.0016) 
    denomtheta ~ dnorm(0, 1)
    theta <- abs(numtheta / denomtheta)

    #2. Likelihood 
    for (i in 1:N){       
      Y[i] ~ dbeta(shape1[i], shape2[i])
      shape1[i] <- theta * pi[i]
      shape2[i] <- theta * (1 - pi[i])
      
      logit(pi[i]) <- eta[i]
      eta[i] <- inprod(beta[], X[i,]) + a[re[i]]

      # Expected value and variance
      ExpY[i] <- pi[i] 
      VarY[i] <- pi[i] * (1 - pi[i]) / (theta + 1)
      PRes[i] <- (Y[i] - ExpY[i]) / sqrt(VarY[i])

      # Posterior predictive check
      YNew[i] ~ dbeta(shape1[i], shape2[i])
      PResNew[i] <- (YNew[i] - ExpY[i]) / sqrt(VarY[i])
      D[i] <- pow(PRes[i], 2)
      DNew[i] <- pow(PResNew[i], 2)
    } 
    
    Fit <- sum(D[1:N])
    FitNew <- sum(DNew[1:N]) 
}

